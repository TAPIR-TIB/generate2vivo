PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>


GENERATE <organization>(?id, ?name, ?established_dtv, ?email, ?website, ?city, ?state, ?country, ?lat, ?lng) {

### id, name : basic attributes of an organization
    <{?id}> a foaf:Organization ;
        rdfs:label ?name .


### established : TRIPLES TO ADD THE DATE THE ORGANIZATION WAS ESTABLISHED
  GENERATE{
    <{?id}> vivo:dateTimeInterval <{?id}-dti> .

    <{?id}-dti> a vivo:DateTimeInterval ;
        vivo:start <{?id}-dtv> .

    <{?id}-dtv> a vivo:DateTimeValue ;
        vivo:dateTime ?established_dtv ;
        vivo:dateTimePrecision vivo:yearPrecision .
  }
  WHERE {
     FILTER( BOUND(?established_dtv) ).
  } .


### vcard : TRIPLES TO ADD VCARD
  GENERATE{
    <{?id}> obo:ARG_2000028 <{?id}-vcard> .

    <{?id}-vcard> a vcard:Individual ;
        obo:ARG_2000029 <{?id}> .
  }
  WHERE {
     FILTER( BOUND(?email) || BOUND(?website) || BOUND(?city) || BOUND(?state) || BOUND(?country) || (BOUND(?lat) && BOUND(?lng))).
  } .


### email : TRIPLES TO ADD EMAIL
  GENERATE{
    <{?id}-vcard> vcard:hasEmail <{?id}-vcard-email> .

    <{?id}-vcard-email> a vcard:Email ;
        vcard:email ?email .
  }
  WHERE {
     FILTER( BOUND(?email) ).
  } .


### website : TRIPLES TO ADD WEBSITE
  GENERATE{
    <{?id}-vcard> vcard:hasURL <{?id}-vcard-url> .

    <{?id}-vcard-url> a vcard:URL ;
      vivo:rank 1 ;
      vcard:url "{?website}"^^xsd:anyURI .
  }
  WHERE {
     FILTER( BOUND(?website) ).
  } .


### address (city, state, country) : TRIPLES TO ADD ADDRESS DATA
  GENERATE{
     <{?id}-vcard> vcard:hasAddress <{?id}-vcard-address> .

    <{?id}-vcard-address> a vcard:Address ;
      vcard:locality ?city ;
      vcard:region ?state ;
      vcard:country ?country .
  }
  WHERE {
     FILTER( BOUND(?city) || BOUND(?state) || BOUND(?country) ).
  } .


### geo (lat,lng) : TRIPLES TO ADD LATITUDE & LONGITUDE
  GENERATE{
    <{?id}-vcard> vcard:hasGeo <{?id}-vcard-geo> .

    <{?id}-vcard-geo> a vcard:Geo ;
        vcard:geo "geo:{?lat},{?lng}" .
  }
  WHERE {
     FILTER( BOUND(?lat) && BOUND(?lng)  ).
  } .

}
WHERE {
  FILTER( BOUND(?id) ).
}
