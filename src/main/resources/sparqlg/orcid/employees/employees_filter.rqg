PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>

## get all people affiliated with the organization
GENERATE <employees_filter>(?orga_id, ?ringgold_id, ?grid_id){

    ## get orcid data for each person
    GENERATE{

        ### for every person check if current employment and if yes, create triples
        ## current = employees where orga_id is same as ringgold or grid AND no end-date
        GENERATE {
            ## create person in vivo ontology, if current employee
            GENERATE <https://projects.tib.eu/tapir/orcid/person/person_mapping.rqg>(?orcid_data) .


            ## connect person to organization via current employment
            GENERATE <https://projects.tib.eu/tapir/orcid/employees/employment_mapping.rqg>(?person_id, ?orga_id, ?employment) .
        }
        ITERATOR iter:JSONPath(?orcid_data, '$.activities-summary.employments.affiliation-group[*]') AS ?employment
        WHERE{
            BIND(<https://projects.tib.eu/tapir/orcid/person/person_id.rqg>(?orcid_data) AS ?person_id)

            BIND(fun:JSONPath(?employment, '$.summaries[0].employment-summary.organization.disambiguated-organization.disambiguated-organization-identifier' ) AS ?disamb_orga)
            BIND(fun:JSONPath(?employment, '$.summaries[0].employment-summary.end-date' ) AS ?end_date)
            FILTER( !BOUND(?end_date) && (?disamb_orga = ?grid_id || ?disamb_orga = ?ringgold_id) ).
        }.

    }
    ITERATOR iter:JSONPath(?orcid_employees, '$.expanded-result[*]') AS ?person
    WHERE{
        BIND(fun:JSONPath(?person, '$.orcid-id') AS ?orcid)

        # query person data from ORCID as json
        BIND(<https://pub.orcid.org/v3.0/{?orcid}> as ?orcid_url)
        BIND( fun:HTTPGet(?orcid_url, "Accept: application/json" ) AS ?orcid_response )
        FILTER( fun:HTTPExtractResponseCode( ?orcid_response ) = 200 )
        BIND( fun:HTTPExtractBody( ?orcid_response ) AS ?orcid_data )
    } .
}
WHERE{
  ### call orcid with ringgold and grid-id for affiliated employees
  BIND(IF(BOUND(?ringgold_id) && BOUND(?grid_id), "%20OR%20", "") AS ?connector)
  BIND(IF(BOUND(?ringgold_id), "ringgold-org-id:{?ringgold_id}", "") AS ?ringgold_search)
  BIND(IF(BOUND(?grid_id), "grid-org-id:{?grid_id}", "") AS ?grid_search)

  BIND("https://pub.orcid.org/v3.0/expanded-search/?q={?ringgold_search}{?connector}{?grid_search}" as ?orcid_employees_url)
  BIND(fun:HTTPGet(IRI(?orcid_employees_url), "Accept: application/json") AS ?orcid_employees_response)
  FILTER(fun:HTTPExtractResponseCode( ?orcid_employees_response ) = 200)
  BIND(fun:HTTPExtractBody( ?orcid_employees_response ) AS ?orcid_employees)
}