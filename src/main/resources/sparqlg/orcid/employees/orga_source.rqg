PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>

GENERATE <orga_source>(?wikidata) {

     GENERATE <https://projects.tib.eu/tapir/orcid/employees/currentEmployees.rqg>( ?grid_id, ?ringgold_id, ?orcid )
     ITERATOR iter:JSONPath(?body, '$.expanded-result[*]') AS ?person
     WHERE{
       BIND(fun:JSONPath(?person, '$.orcid-id') AS ?orcid)
     }.

}
SOURCE <https://www.wikidata.org/w/api.php?action=wbgetentities&ids={?wikidata}&props=claims&format=json> AS ?orga
WHERE{
  # get organization parameters from wikidata
  BIND(fun:JSONPath(?orga, '$.entities.{?wikidata}.claims.P3500[0].mainsnak.datavalue.value' ) AS ?ringgold_id)
  BIND(fun:JSONPath(?orga, '$.entities.{?wikidata}.claims.P2427[0].mainsnak.datavalue.value' ) AS ?grid_id)

  # call orcid via http get request with ringgold and grid-id
  BIND(<https://pub.orcid.org/v3.0/expanded-search/?q=ringgold-org-id:{?ringgold_id}%20OR%20grid-org-id:{?grid_id}> as ?orcid_url)
  BIND(fun:HTTPGet(?orcid_url, "Accept: application/json") AS ?response)
  FILTER(fun:HTTPExtractResponseCode( ?response ) = 200)
  BIND(fun:HTTPExtractBody( ?response ) AS ?body)
}