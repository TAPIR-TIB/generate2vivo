BASE <http://vivo.mydomain.edu/individual/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

GENERATE  <orga2person>(?ror) {

    <{?rorid}> a foaf:Organization ;
               rdfs:label ?name .

}
SOURCE <https://projects.tib.eu/tapir/graphql/orga2person> AS ?graphqlquery
WHERE {
    #replace whitespace in graphql (compress message)
    BIND(REPLACE(?graphqlquery, "\\s+", " ") AS ?graphql)
    BIND ("""\{"variables":\{"ror":"{?ror}"},"query":"{?graphql}"}""" AS ?postbody)


  BIND(  fun:HTTPPost(<https://api.datacite.org/graphql>, "Content-type: application/json" , ?postbody) AS ?response )
  FILTER( fun:HTTPExtractResponseCode( ?response ) = 200 )
  BIND( fun:HTTPExtractBody( ?response ) AS ?body )
  BIND( fun:JSONPath( ?body, "$.data.organization" ) AS ?orga )

   # basic properties of organization in json
   BIND(REPLACE(fun:JSONPath(?orga, '$.id' ), "https://ror.org/" , "" ) AS ?rorid)
   BIND(fun:JSONPath(?orga, '$.name' ) AS ?name)
}
