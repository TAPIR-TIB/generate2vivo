BASE <http://vivo.mydomain.edu/individual/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

GENERATE {

## triples for person
    <{?orcid}> a foaf:Person ;
        rdfs:label ?completeName ;
        vivo:orcidId <https://orcid.org/{?orcid}> ;
        obo:ARG_2000028 <{?orcid}-vcard> .

    <https://orcid.org/{?orcid}> a owl:Thing ;
        vivo:confirmedOrcidId <{?orcid}> .

    <{?orcid}-vcard> a vcard:Individual ;
        obo:ARG_2000029 <{?orcid}> ;
        vcard:hasName <{?orcid}-vcard-name> .

    <{?orcid}-vcard-name> a vcard:Name ;
        vcard:familyName ?familyName ;
        vcard:givenName ?givenName .

## connection person -> publication
    <{?orcid}-to-{?doi_clean}> a vivo:Authorship ;
        vivo:relates <{?orcid}> ;
        vivo:relates <{?doi_clean}> .

## triples for publication
    <{?doi_clean}> a ?pubtype ;
        rdfs:label ?title ;
        bibo:doi ?doi ;
        vivo:dateTimeValue <{?doi_clean}-dtv> .

    <{?doi_clean}-dtv> a vivo:DateTimeValue ;
        vivo:dateTime ?pubyear_dtv ;
        vivo:dateTimePrecision vivo:yearPrecision .

}
SOURCE <http://tib.eu/default.json> AS ?source
ITERATOR iter:JSONPath(?source, '$.data.person') AS ?person
ITERATOR iter:JSONPath(?person, '$.publications.edges[*].node') AS ?pub
WHERE {
   # basic properties of a person in json
   BIND(REPLACE(fun:JSONPath(?person, '$.id' ), "https://orcid.org/" , "" ) AS ?orcid)
   BIND(fun:JSONPath(?person, '$.givenName' ) AS ?givenName)
   BIND(fun:JSONPath(?person, '$.familyName' ) AS ?familyName)
   # processed properties
   BIND(CONCAT(?familyName, ", ", ?givenName)  AS ?completeName)

   # basic properties of a publication in json
   BIND(fun:JSONPath(?pub, '$.doi' ) AS ?doi)
   BIND(REPLACE(?doi, "/", "") AS ?doi_clean)
   BIND(fun:JSONPath(?pub, '$.titles[0].title' ) AS ?title)

   BIND(fun:JSONPath(?pub, '$.publicationYear' ) AS ?pubyear)
   BIND(xsd:dateTime(CONCAT(STR(?pubyear), "-01-01T00:00:00"))  AS ?pubyear_dtv)

   BIND(fun:JSONPath(?pub, '$.types.citeproc' ) AS ?citeproc)
   # mapping taken from VIVO (claim DOI)
   BIND (
     IRI(COALESCE(
       IF(?citeproc = "article-journal", bibo:AcademicArticle, 1/0),
       IF(?citeproc = "article", bibo:Article, 1/0),
       IF(?citeproc = "book", bibo:Book, 1/0),
       IF(?citeproc = "chapter", bibo:Chapter, 1/0),
       IF(?citeproc = "dataset", vivo:Dataset, 1/0),
       IF(?citeproc = "figure", bibo:Image, 1/0),
       IF(?citeproc = "graphic", bibo:Image, 1/0),
       IF(?citeproc = "legal_case", bibo:LegalCaseDocument, 1/0),
       IF(?citeproc = "legislation", bibo:Legislation, 1/0),
       IF(?citeproc = "manuscript", bibo:Manuscript, 1/0),
       IF(?citeproc = "map", bibo:Map, 1/0),
       IF(?citeproc = "musical_score", vivo:Score, 1/0),
       IF(?citeproc = "paper-conference", vivo:ConferencePaper, 1/0),
       IF(?citeproc = "patent", bibo:Patent, 1/0),
       IF(?citeproc = "personal_communication", bibo:PersonalCommunicationDocument, 1/0),
       IF(?citeproc = "post-weblog", vivo:BlogPosting, 1/0),
       IF(?citeproc = "report", bibo:Report, 1/0),
       IF(?citeproc = "review", vivo:Review, 1/0),
       IF(?citeproc = "speech", vivo:Speech, 1/0),
       IF(?citeproc = "thesis", bibo:Thesis, 1/0),
       IF(?citeproc = "webpage", bibo:Webpage, 1/0),

     # mapping leftover types
       IF(STRSTARTS(?citeproc, "article"), bibo:Article, 1/0),
       IF(?citeproc = "bill", bibo:Bill, 1/0),
       IF(STRSTARTS(?citeproc, "entry"), bibo:DocumentPart, 1/0),
       IF(?citeproc = "interview", bibo:Interview, 1/0),
       IF(?citeproc = "motion_picture", bibo:Film, 1/0),
       IF(?citeproc = "post", vivo:BlogPosting, 1/0),
       IF(?citeproc = "review-book", vivo:Review, 1/0),
       IF(?citeproc = "song", bibo:AudioDocument, 1/0),
       IF(?citeproc = "treaty", bibo:LegalDocument, 1/0),

       bibo:Document  #default if nothing else fits
     )) AS ?pubtype
   )
}
