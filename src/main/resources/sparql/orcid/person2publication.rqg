BASE <http://vivo.mydomain.edu/individual/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

GENERATE {

## triples for person
    <{?orcid}> a foaf:Person ;
        rdfs:label ?completeName ;
        vivo:orcidId <https://orcid.org/{?orcid}> ;
        obo:ARG_2000028 <{?orcid}-vcard> .

    <https://orcid.org/{?orcid}> a owl:Thing ;
        vivo:confirmedOrcidId <{?orcid}> .

    <{?orcid}-vcard> a vcard:Individual ;
        obo:ARG_2000029 <{?orcid}> ;
        vcard:hasName <{?orcid}-vcard-name> .

    <{?orcid}-vcard-name> a vcard:Name ;
        vcard:familyName ?familyName ;
        vcard:givenName ?givenName .

## connection person -> publication
    <{?orcid}-to-{?doi}> a vivo:Authorship ;
        vivo:relates <{?orcid}> ;
        vivo:relates <{?doi}> .

## triples for publication
    <{?doi}> a <http://purl.org/ontology/bibo/Article> ; ####
        rdfs:label ?title ;
        bibo:doi ?doi ;
        vivo:dateTimeValue <{?doi}-dtv> .

    <{?doi}-dtv> a vivo:DateTimeValue ;
        vivo:dateTime ${ fun:dateTime( ?pubyear_dtv ) };
        vivo:dateTimePrecision vivo:yearPrecision .

}
SOURCE <http://tib.eu/default.json> AS ?source
ITERATOR iter:JSONPath(?source, '$.data.person') AS ?person
ITERATOR iter:JSONPath(?person, '$.publications.edges[*].node') AS ?pub
WHERE {
   # basic properties of a person in json
   BIND(REPLACE(fun:JSONPath(?person, '$.id' ), "https://orcid.org/" , "" ) AS ?orcid)
   BIND(fun:JSONPath(?person, '$.givenName' ) AS ?givenName)
   BIND(fun:JSONPath(?person, '$.familyName' ) AS ?familyName)
   # processed properties
   BIND(CONCAT(?familyName, ", ", ?givenName)  AS ?completeName)

   # basic properties of a publication in json
   BIND(fun:JSONPath(?pub, '$.doi' ) AS ?doi)
   BIND(fun:JSONPath(?pub, '$.titles[0].title' ) AS ?title)
   BIND(fun:JSONPath(?pub, '$.publicationYear' ) AS ?pubyear)
   #processed properties
   BIND(CONCAT(?pubyear, "-01-01T00:00:00")  AS ?pubyear_dtv)
}
